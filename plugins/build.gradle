buildscript {
    apply from: "../core/gradle/versions.gradle"
    apply from: "../android/gradle/versions.gradle"

    repositories {
        google()
        gradlePluginPortal()
    }
    dependencies {
        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // Dokka
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        // Android
        classpath "com.android.tools.build:gradle:7.0.4"
    }
}

plugins {
    id 'io.github.gradle-nexus.publish-plugin' version "$publish_plugin_version"
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            username = findProperty('OSSRH_USERNAME') ?: System.getenv('OSSRH_USERNAME') ?: ""
            password = findProperty('OSSRH_PASSWORD') ?: System.getenv('OSSRH_PASSWORD') ?: ""
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
allprojects {

    repositories {
        mavenLocal()
        google()
    }

    group = 'io.insert-koin'
    version = "$koin_version"

    // Dokka
    apply plugin: 'org.jetbrains.dokka'
    dokkaJavadoc {
    }
    task dokkaJar(type: Jar) {
        classifier = 'javadoc'
        from dokkaJavadoc
    }
}
