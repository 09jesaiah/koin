buildscript {
    apply from: "../gradle/versions.gradle"
    apply from: "../gradle/versions-android.gradle"

    repositories {
        google()
        jcenter()
    }
    dependencies {
        // Kotlin
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        // Bintray
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:$bintray_version"
        // Dokka
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        // Android
        classpath 'com.android.tools.build:gradle:7.0.0-alpha04'
//        classpath "com.android.tools.build:gradle:$android_gradle_version"
        classpath "digital.wup:android-maven-publish:$android_maven_publish"
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
allprojects {

    repositories {
        mavenLocal()
        google()
        jcenter()
    }

    group = 'org.koin'
    version = "$koin_version"

    // Dokka
    apply plugin: 'org.jetbrains.dokka'
    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }
    task dokkaJar(type: Jar) {
        classifier = 'javadoc'
        from dokka
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = '1.8'
            apiVersion = '1.4'
            languageVersion = '1.4'
            freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
        }
    }
}